plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.12'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Holiday-Keeper'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {

    // Spring Boot Starter
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webflux' // WebClient
    implementation 'org.springframework.boot:spring-boot-starter-batch'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Querydsl - JPA (jakarta 버전)
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'

    // Querydsl APT (Q클래스 생성용)
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // H2 Database (runtime)
    runtimeOnly 'com.h2database:h2'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

// Q-Class 생성할 디렉토리 경로
def queryDslSrcDir = 'src/main/java/com/example/holidaykeeper/generated/querydsl/'

// JavaCompile Task 수행 시 생성되는 소스 코드 출력 디렉토리를 queryDslSrcDir로 설정
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.getGeneratedSourceOutputDirectory().set(file(queryDslSrcDir))
}

// 소스 코드로 인식할 디렉토리 경로에 Q-Class 파일 추가.
// Q-Class가 일반 java class로 취급되어 컴파일 & 실행 시 classpath에 추가
sourceSets {
    main.java.srcDirs += [queryDslSrcDir]
}

// clean task 수행 시 지정한 디렉토리 삭제 : Q-Class 제거
clean{
    delete file(queryDslSrcDir)
}
tasks.named('test') {
    useJUnitPlatform()
}
